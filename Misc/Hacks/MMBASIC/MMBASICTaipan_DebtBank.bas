1 REM TAIPAN! FOR MMBASIC
2 OPTION ESCAPE: CLRSCRN$="\027[2J":FONTX = MM.INFO(FONTWIDTH) : FONTY = MM.INFO(FONTHEIGHT):MODE 1:REM MMBASIC SPECIFICS

5 REM INITIALIZATION (10-50)
10 HOME=0:PRINT CLRSCRN$: ABLANK$ = "                                        ":W$ = "ELDER BROTHER WU":LY$ = "LI YUEN":YS$ = "YANATO & SMYTHE":TC$ = "O, S, T, A, P, OR R"
11 BLANK$="                                       "

20 VTAB=4: INVERSE=0:PRINT @(0*FONTX,4*FONTY)ABLANK$:HTAB=13:PRINT @(13*FONTX,5*FONTY)"T A I P A N:": NORMAL=0:HTAB=13:PRINT@(13*FONTX,6*FONTY)"_________________"
21 SPEED = 100: VTAB=9:HTAB=14:PRINT@(14*FONTX,9*FONTY)"A  G A M E  I N":PRINT TAB( 15);"C O N T E X T": PRINT:PRINT TAB( 14);"HAYDEN BOOK CO."
22 SPEED = 255: VTAB=15: INVERSE=0:PRINT @(0*FONTX,15*FONTY)ABLANK$: NORMAL=0

30 DIM MON$(11),GOODS$(5),GG(5),LOW(9,5),HIGH(9,5),GP(5),VISITS(9),LOC$(9),SG(5):L=0:FOR I = 0 TO 9:READ LOC$(I):NEXT I:FOR I = 0 TO 11: READ MON$(I):NEXT I:FOR I = 0 TO 5: READ GOODS$(I):NEXT I

40 REM FOR I = 0 TO 9: FOR J = 0 TO 5:READ AP(I,J):AP(I,J) = AP(I,J) * 6 ^ (5 - J): NEXT J ,I: GOSUB 180
41 DIM INTEGER AP(9,5) = (8,2,2,4,5,2,8,2,1,8,4,3,9,3,3,6,5,4,6,4,2,8,6,5,3,3,4,7,4,4,2,4,5,4,3,5,3,4,5,5,2,6,3,1,4,5,3,3,1,5,6,3,4,5,3,9,9,1,9,5)
42 FOR I = 0 TO 9: FOR J = 0 TO 5:AP(I,J) = AP(I,J) * 6 ^ (5 - J): NEXT J ,I: GOSUB 180

44 BANK = 0 

45 PRINT CLRSCRN$: PRINT @(0,6*FONTY) "Do you wish to start . . .": PRINT: PRINT "  1) With cash (and a debt)": PRINT: PRINT "                >> or <<":PRINT: PRINT "  2) With five guns and no cash": PRINT "                (But no debt)":REM NO DEBT
46 GOSUB 60: IF KEY$="2" THEN DIM INTEGER LO(9) = (21,14,7,0,35,49,56,42,84,200):D = 0: Y=1860: GT = 1: C=0:MW = 50:SH = MW:SR = 1: G=5:VISITS(0) = 1: GOSUB 5000: KEY$ ="":GOSUB 590: HOME=0:PRINT CLRSCRN$:GOTO 120:REM NO DEBT
47 IF KEY$ <> "1" THEN GOTO 46:REM NO DEBT

50 REM FOR I = 0 TO 9:READ LO(I):NEXT I:D = 1000: Y=1860: GT = 1: C=400:MW = 50:SH = MW:SR = 1: G=1:VISITS(0) = 1: GOSUB 5000: KEY$ ="":GOSUB 590: HOME=0:PRINT CLRSCRN$:GOTO 120
51 DIM INTEGER LO(9) = (21,14,7,0,35,49,56,42,84,200) : D = 1000: Y=1860: GT = 1: C=400:MW = 50:SH = MW:SR = 1: G=1:VISITS(0) = 1: GOSUB 5000: KEY$ = "":GOSUB 590: HOME=0:PRINT CLRSCRN$:GOTO 120

55 REM GET$ SUBROUTINE (60-64)
60 REM POKE -16368, 0
61 REM IF PEEK ( -16384) < 128 THEN GOTO 61
62 REM KEY$ = CHR$ ( PEEK ( -16384) - 128) :POKE -16368, 0
63 KEY$ = INKEY$: IF KEY$="" THEN GOTO 63
64 RETURN

65 REM INITIALIZATION DATA (70-110)
70 DATA HONGKONG, FOOCHOU, SHANGHAI, NAGASAKI, MANILA, SINGAPORE, BATAVIA, SAIGON, CALCUTTA, LIVERPOOL

80 DATA JAN, FEB, MAR, APR, MAY, JUN
81 DATA JUL, AUG, SEP, OCT, NOV, DEC
90 DATA OPIUM, SILK, TEA, ARMS, PEPPER, RICE
100 REM DATA 8,2,2,4,5,2,8,2,1,8,4,3,9,3,3,6,5,4,6,4,2,8,6,5,3,3,4,7,4,4,2,4,5,4,3,5,3,4,5,5,2,6,3,1,4,5,3,3,1,5,6,3,4,5,3,9,9,1,9,5
110 REM DATA 21,14,7,0,35,49,56,42,84,200


115 REM MAIN DISPLAY (120-150)
120 GOSUB 130: GOTO 220
130 VTAB=1: HTAB=1:PRINT @(1*FONTX,1*FONTY) "PORT ";LOC$(L);: HTAB=28:PRINT @(28*FONTX, 1*FONTY) MON$(M);". ";DA+1;",";Y


141 VTAB=2: INVERSE=0:PRINT @(0*FONTX,2*FONTY) "CASH ";: Q = C:GOSUB 1330: PRINT @(15*FONTX,2*FONTY) "BANK ";: Q = BANK:GOSUB 1330
142 NORMAL=0:VTAB=2:HTAB=28:PRINT @(28*FONTX,2*FONTY) "GUNS ";G: VTAB=3
143 PRINT @(0*FONTX,3*FONTY) "DEBT ";: Q = D:GOSUB 1330:VTAB=3:HTAB=28:PRINT @(28*FONTX,3*FONTY) "HOLD ";: Q = SH:GOSUB 1330


145 VTAB=4: INVERSE=0:PRINT @(0*FONTX,4*FONTY) "GOODS     ABOARD SHIP    HONGKONG GODOWN":NORMAL=0


151 FOR I = 0 TO 5: VTAB=5 + I:PRINT @(0*FONTX,(5+I)*FONTY) GOODS$(I):VTAB=5 + I:HTAB=11
152 PRINT @(11*FONTX,(5+I)*FONTY) CHR$ (124);: Q=SG(I):GOSUB 1330
153 VTAB=5 + I:HTAB=26:PRINT @(26*FONTX,(5+I)*FONTY) CHR$ (124);
154 Q = GG(I):GOSUB 1330: NEXT I: INVERSE=0: PRINT ABLANK$: NORMAL=0: RETURN



155 REM UPDATE SUBROUTINE (160-172)
160 ET = INT (ET + (ET * RND (1) / 3)): GT = GT + ET:D = D + INT (D * (ET / 360)):JD = JD + ET:IF JD > 360 THEN JD = JD-360: Y=Y+1
161 IF JD < 1 THEN JD = 1
165 BANK = INT (BANK + BANK * (0.005/30)*ET)

170 M= INT ((JD - 30) / 30): DA = INT ((((JD / 30) -INT(JD/30)))*30):IF RND (1) > .95 THEN TR = 0



171 IF M < 0 THEN M = 0
172 RETURN

175 REM PRICE VARIATION SUBROUTINE (180)
180 FOR I = 0 TO 5:GP(I) = INT (AP(L,I) + ( RND (1) * AP(L,I))):NEXT I: RETURN

185 REM HI-LO SUBROUTINE (190-210)
190 FOR I = 0 TO 5:IF GP(I) > HIGH(L,I) THEN HIGH(L,I) = GP(I)
200 IF GP(I) < LOW(L,I) OR LOW(L,I)=0 THEN LOW(L,I)= GP (I)
210 NEXT I: RETURN

212 REM MARKET (220-350)
217 REM MARKET PRICES (220-221)
220 GOSUB 790: GOSUB 1340:VTAB=11:INVERSE=0:HTAB=8:PRINT @(8*FONTX,11*FONTY) " ";LOC$(L);" MARKET PRICES ":NORMAL=0:PRINT ABLANK$
221 FOR I = 0 TO 4 STEP 2: VTAB=13 + I / 2:HTAB=1:PRINT @(1*FONTX,(13+I/2)*FONTY) GOODS$(I);: HTAB=10:PRINT TAB(10) GP(I) ;: HTAB=21:PRINT TAB(21) GOODS$(I + 1);
222 HTAB=30:PRINT TAB(30) GP(I + 1) :NEXT I
225 REM MARKET MENU (230-244)
230 PRINT: VTAB=18:PRINT @(0*FONTX,18*FONTY) " B)UY, S)ELL, L)EAVE, OR R)ETIRE?" 
235 REM MARKET MENU INPUT (240-244)


240 GOSUB 60: IF KEY$= "B" OR KEY$="b" THEN T$ = "BUY":X=1
241 IF KEY$= "S" OR KEY$="s" THEN T$ = "SELL":X=2

242 IF KEY$ = "L" OR KEY$="l" THEN GOTO 360
243 IF KEY$ = "R" OR KEY$="r" THEN GOTO 1300
244 IF KEY$ <> "B" AND KEY$ <> "S" AND KEY$ <> "L" AND KEY$ <> "R" AND KEY$ <> "b" AND KEY$ <> "s" AND KEY$ <> "l" AND KEY$ <> "r" THEN GOSUB 770:GOTO 240
247 REM ITEM MENU (250)
250 VTAB=18:PRINT @(0*FONTX,18*FONTY) ABLANK$;:VTAB=18:HTAB=3:PRINT @(3*FONTX,18*FONTY) T$;" ";TC$;"?": GOSUB 260:GOTO 280
255 REM ITEM CHOICE SUBROUTINE (260-270)
260 GOSUB 60:IF KEY$ = "O" OR KEY$="o" THEN X1 = 0
261 IF KEY$ = "S" OR KEY$="s" THEN X1 = 1
262 IF KEY$ = "A" OR KEY$="a" THEN X1 = 3 
263 IF KEY$ = "P" OR KEY$="p" THEN X1 = 4 
264 IF KEY$ = "T" OR KEY$="t" THEN X1 = 2 
265 IF KEY$ = "R" OR KEY$="r" THEN X1 = 5
266 IF KEY$ <> "O" AND KEY$ <> "S" AND KEY$ <> "T" AND KEY$ <>"A" AND KEY$ <> "P" AND KEY$ <> "R" AND KEY$ <> "o" AND KEY$ <> "s" AND KEY$ <> "t" AND KEY$ <>"a" AND KEY$ <> "p" AND KEY$ <> "r" THEN GOSUB 770:GOTO 260
270 RETURN

280 IF X=1 AND GP(X1) > C THEN VTAB=18:PRINT @(0*FONTX,18*FONTY) "YOU CAN'T AFFORD ANY ";GOODS$(X1);". ";: GOSUB 760:GOTO 230

290 IF X = 2 AND SG(X1) < 1 THEN VTAB=18: PRINT @(0*FONTX,18*FONTY) "YOU HAVE NO ";GOODS$(X1); " ABOARD!      ";: GOSUB 760:GOTO 230
300 VTAB=18: PRINT @(0*FONTX,18*FONTY)ABLANK$;:VTAB=18:PRINT @(0*FONTX,18*FONTY) T$;" HOW MUCH ";GOODS$(X1);: PRINT "? ";:NUMBER$ = "":NUM = 0:GOSUB 310:GOTO 320

305 REM SOOPER DOOPER NUMBER SCOOPER SUBROUTINE (310-314)
310 PRINT "\b";: INVERSE=0:PRINT " ";:NORMAL=0:GOSUB 60:IF LEN (NUMBER$) > 0 AND ASC (KEY$) = 8 THEN PRINT KEY$;: PRINT" "; :PRINT KEY$;KEY$;:PRINT" ";:FG=1:IF LEN (NUMBER$) = 1 THEN NUMBER$ = "": FG = 0:GOTO 310

311 IF FG = 1 THEN NUMBER$ = LEFT$ (NUMBER$, LEN (NUMBER$) - 1):FG = 0: GOTO 310
312 IF ASC (KEY$) = 65 OR ASC (KEY$) = 97 OR ASC (KEY$) = 13 THEN FOR LOOP=0 TO LEN(NUMBER$): PRINT "\b \b";: NEXT LOOP: RETURN
313 IF ASC (KEY$) < 48 OR ASC (KEY$) > 57 THEN GOTO 310
314 NUMBER$ = NUMBER$ + KEY$: PRINT "\b";KEY$; : INVERSE=0:PRINT " ";:NORMAL=0: GOTO 310
320 IF X= 1 THEN IF KEY$ = "A" OR KEY$="a" THEN PRINT "\b";: INVERSE=0:PRINT " ALL"; :NUM = INT (C / GP(X1))
321 IF X = 1 AND KEY$ <> "A" AND KEY$ <> "a" THEN NUM = VAL (NUMBER$)
330 IF X = 2 THEN IF KEY$ = "A" OR KEY$="a" THEN PRINT "\b" ; : INVERSE=0:PRINT " ALL";:NUM = SG(X1)
331 IF X = 2 AND KEY$ <> "A" AND KEY$ <> "a" THEN NUM = VAL (NUMBER$) 
335 NORMAL=0

340 IF X = 1 AND NUM * GP(X1) > C THEN PRINT :VTAB=18:PRINT @(0*FONTX,18*FONTY) "YOU CAN'T AFFORD SO MUCH! ";: GOSUB 760:VTAB=18:PRINT @(0*FONTX,18*FONTY) ABLANK$;:GOTO 300
341 IF X = 1 AND NUM * GP(X1) <= C THEN SG(X1) = SG(X1) + NUM: SH=SH- NUM:C = C - GP(X1) * NUM:PRINT @(0*FONTX,19*FONTY) ABLANK$;:GOSUB 130: GOTO 230
350 IF NUM > SG(X1) THEN VTAB=18: PRINT @(0*FONTX,18*FONTY) ABLANK$;:VTAB=18:PRINT @(0*FONTX,18*FONTY) "YOU DON'T HAVE THAT MUCH! ";: GOSUB 760:VTAB=18:PRINT @(0*FONTX,18*FONTY) ABLANK$ ;:GOTO 300
351 SG(X1) = SG(X1) - NUM: SH = SH + NUM:C = C + (NUM * GP(X1) ) :PRINT @(0*FONTX,19*FONTY) ABLANK$;: GOSUB 130:GOTO 230

355 REM OTHER OPTIONS (360-381)
360 GOSUB 130: GOSUB 1340:VTAB=11:INVERSE=0:PRINT @(0*FONTX,11*FONTY) ABLANK$:NORMAL=0:PRINT " T)RADE, R)ECORDS" ; :IF L = 0 THEN PRINT ", L)ENDER, G)ODOWN, B)ANK OR E)MBARK?":PRINT: PRINT ABLANK$
361 IF L <> 0 THEN PRINT " OR E)MBARK?": PRINT:PRINT: PRINT ABLANK$;
370 GOSUB 60:IF (KEY$ = "G" OR KEY$ = "g") AND L = 0 THEN X = 1
371 IF KEY$ = "T" OR KEY$ = "t" THEN GOTO 120
372 IF KEY$="R" OR KEY$ = "r" THEN X=2
373 IF KEY$="L" OR KEY$ = "l" THEN X=3
374 IF KEY$="E" OR KEY$ = "e" THEN GOTO 730
375 IF (KEY$="B" OR KEY$ = "b") AND L = 0 THEN X=4
376 IF ((KEY$ <> "G" AND KEY$ <> "g") OR L<>0) AND ((KEY$ <> "B" AND KEY$ <> "b") OR L<>0) AND KEY$ <> "T" AND KEY$<> "R" AND KEY$ <> "L" AND KEY$ <> "E" AND KEY$ <> "t" AND KEY$<> "r" AND KEY$ <> "l" AND KEY$ <> "e" THEN GOSUB 770: GOTO 370

380 IF X = 3 AND L <> Q THEN GOSUB 770: GOTO 370
381 IF X <> 3 OR L = 0 THEN ON X GOSUB 390,600,470,2000: GOTO 360

384 REM GODOWN SUBROUTINE (390-461)
387 REM GODOWN MENU (390-392)
390 GOSUB 130: VTAB=13:PRINT @(0*FONTX,13*FONTY) ABLANK$:VTAB=13:PRINT @(0*FONTX,13*FONTY) "CARGO <T>O OR <F>ROM GODOWN?":PRINT ABLANK$;:GOSUB 60:IF KEY$ = "T" OR KEY$ = "t" THEN GOTO 410
391 IF KEY$="F" OR KEY$="f" THEN GOTO 440
392 IF KEY$<> "T" AND KEY$ <> "F" AND KEY$<> "t" AND KEY$ <> "f" THEN GOSUB 770: GOTO 390

395 REM GODOWN CARGO CHOICE SUBROUTINE (400)
400 VTAB=13:PRINT @(0*FONTX,13*FONTY) "MOVE WHAT ("TC$")? ": GOSUB 260:RETURN


410 GOSUB 400:IF SG(X1) = 0 THEN VTAB=13:PRINT @(0*FONTX,13*FONTY) "YOU HAVE NO ";GOODS$(X1);" ABOARD!        ": GOSUB 760:RETURN
420 NUMBER$ = "":VTAB=13:PRINT @(0*FONTX,13*FONTY) ABLANK$:VTAB=13:PRINT @(0*FONTX,13*FONTY) " MOVE HOW MUCH ";GOODS$(X1);"? ";: GOSUB 310:IF KEY$ = "A" OR KEY$ = "a" THEN INVERSE=0:PRINT "\b";"ALL"; :NUM = SG(X1) :NORMAL=0
421 IF KEY$ <> "A" AND KEY$ <> "a" THEN NUM = VAL (NUMBER$)
425 HTAB=1: PRINT TAB(1);
430 IF NUM > SG(X1) THEN VTAB=13: PRINT @(0*FONTX,13*FONTY) " ONLY ";:Q = SG(X1):GOSUB 1330:HTAB=11:PRINT TAB(11) " UNITS ABOARD!    ": GOSUB 760:GOTO 420

431 IF NUM <= SG(X1) THEN SG(X1) = SG(X1) - NUM: SH = SH + NUM:GG(X1) = GG(X1) + NUM: NUMBER$ = "":RETURN
440 GOSUB 400:IF GG(X1) =0 THEN VTAB=13:PRINT @(0*FONTX,13*FONTY) " THE GODOWN HAS NO ";GOODS$(X1);".           ": GOSUB 760:RETURN
450 NUMBER$ = "": VTAB=13:PRINT @(0*FONTX,13*FONTY) BLANK$;:TAB 1:PRINT " MOVE HOW MUCH " ;GOODS$(X1);" TO SHIP? ";: GOSUB 310:IF KEY$ = "A" OR KEY$ = "a" THEN PRINT "\b";:INVERSE=0:PRINT "ALL" ;:NORMAL=0:NUM = GG(X1)
451 IF KEY$ <> "A" AND KEY$ <> "a" THEN NUM = VAL (NUMBER$)
460 IF NUM > GG(X1) THEN VTAB=13: PRINT @(0*FONTX,13*FONTY) " JUST ";:Q = GG(X1): GOSUB 1330:PRINT " UNITS STORED!":GOSUB 760: GOTO 450

461 IF NUM <= GG(X1) THEN GG(X1) =GG(X1) - NUM: SG(X1) = SG(X1) + NUM:SH=SH- NUM:NUMBER$= "":RETURN

465 REM LENDER SUBROUTINE (470-580)
470 REM GOSUB 130: GOSUB 1340:VTAB=12:PRINT @(0*FONTX,12*FONTY) "[WANCHAI DISTRICT OF HONGKONG: HOME OF "; W$;"]":PRINT W$;" GREETS YOU, TAIPAN, AND WISHES YOU WELL.":GOSUB 780:VTAB=14:PRINT @(0*FONTX,14*FONTY) ABLANK$:PRINT ABLANK$
471 REM GOSUB 130: GOSUB 1340:VTAB=12:PRINT @(0*FONTX,12*FONTY) "[WANCHAI DISTRICT OF HONGKONG:": PRINT "HOME OF "; W$;"]":PRINT W$;" GREETS YOU, TAIPAN,": PRINT "AND WISHES YOU WELL.":GOSUB 780:VTAB=14:PRINT @(0*FONTX,14*FONTY) ABLANK$:PRINT ABLANK$
472 REM GOSUB 130: GOSUB 1340:VTAB=12:PRINT @(0*FONTX,12*FONTY) "[WANCHAI DISTRICT OF HONGKONG: HOME OF "; W$;"]":PRINT @(0*FONTX,14*FONTY) W$;" GREETS YOU, TAIPAN, AND WISHES YOU WELL.":GOSUB 780:VTAB=14:PRINT @(0*FONTX,14*FONTY) ABLANK$:PRINT ABLANK$

473 GOSUB 130: GOSUB 1340:VTAB=12:PRINT @(0*FONTX,12*FONTY) "[WANCHAI DISTRICT OF HONGKONG:": PRINT "HOME OF "; W$;"]":PRINT @(0*FONTX,14*FONTY) W$;" GREETS YOU, TAIPAN,": PRINT "AND WISHES YOU WELL.":GOSUB 780:VTAB=14:PRINT @(0*FONTX,14*FONTY) ABLANK$:PRINT ABLANK$

480 VTAB=14:PRINT @(0*FONTX,14*FONTY) "WU STATES THAT HIS IRON LOTUS": PRINT "TRIAD HAS BEEN WATCHING YOU.": GOSUB 780
490 VTAB=14: PRINT @(0*FONTX,14*FONTY) ABLANK$: PRINT ABLANK$: PRINT ABLANK$:VTAB=14:PRINT @(0*FONTX,14*FONTY) W$;" ASKS, DO YOU":PRINT ABLANK$;:VTAB=15:PRINT @(0*FONTX,15*FONTY) "WISH TO B)ORROW, P)AY, OR Q)UIT?    ";: NUMBER$ = ""
500 GOSUB 60:IF KEY$ = "B" OR KEY$ = "b" THEN LDACT$ = "BORROW": LD = 1
501 IF KEY$ = "P" OR KEY$ = "p" THEN LDACT$ = "PAY": LD = 2
502 IF KEY$ = "Q" OR KEY$ = "q" THEN PRINT: GOSUB 1340:RETURN
503 IF KEY$ <> "B" AND KEY$ <> "P" AND KEY$ <> "Q" AND KEY$ <> "b" AND KEY$ <> "p" AND KEY$ <> "q" THEN GOSUB 770: GOTO 500
510 IF LD = 1 AND (B = 1 OR D > 1E4) THEN GOSUB 1340: VTAB=15:PRINT @(0*FONTX,15*FONTY) "WU REGRETS THAT HE CANNOT LOAN": PRINT "YOU MORE AT THIS TIME, TAIPAN.";: GOSUB 760:GOTO 490

520 GOSUB 1340: VTAB=15:PRINT @(0*FONTX,15*FONTY) "HOW MUCH DO YOU WISH TO ";LDACT$;",":PRINT "TAIPAN? ";:GOSUB 310:NUM = VAL (NUMBER$)


530 IF (KEY$ = "A" OR KEY$ = "a") AND LD = 1 THEN PRINT "\b";: INVERSE=0:PRINT "ALL"; : NORMAL=0:NUM = C * 2
531 IF (KEY$ = "A" OR KEY$ = "a") AND LD = 2 THEN PRINT "\b";: INVERSE=0:PRINT "ALL";: NORMAL=0:NUM = D
540 IF LD = 2 THEN IF C > 0 AND NUM > 0 THEN GOTO 560 ELSE GOSUB 760: GOTO 490
541 IF NUM > 2 * C THEN VTAB=15: PRINT @(0*FONTX,15*FONTY) ABLANK$:VTAB=15:PRINT @(0*FONTX,15*FONTY) W$;" REGRETS THAT HE":PRINT "CANNOT LOAN YOU THAT MUCH.";: GOSUB 760:GOTO 490
550 B=1:C = C + NUM:D = D + NUM: GOSUB 130: GOTO 490
560 IF NUM > C THEN NUM = C: D = D - C:C=0:GOSUB 130:VTAB=15:PRINT @(0*FONTX,15*FONTY) W$;" THANKS YOU,":PRINT "TAIPAN, FOR THE PAYMENT.";: GOSUB 780:IF D < 0 THEN D = 0:GOSUB 130:GOTO 490
561 IF NUM > C AND D >= 0 THEN GOSUB 130: GOTO 490
570 IF NUM > D THEN D = 0: C = C - NUM:VTAB=15:PRINT @(0*FONTX,15*FONTY) W$;" THANKS YOU FOR YOUR STARTLING GENEROSITY!";:GOSUB 130:GOSUB 780:GOTO 490
580 C = C - NUM: D = D - NUM:GOSUB 130:VTAB=15:PRINT @(0*FONTX,15*FONTY) W$;" ACCEPTS YOUR PAYMENT": PRINT "WITH GRATITUDE, TAIPAN   ";:GOSUB 780: GOTO 490

585 REM RANDOMIZER SUBROUTINE (590-591)
590 VTAB=15: HTAB=8:PRINT @(8*FONTX,15*FONTY) "PRESS <";: FLISH=0:PRINT "SPACEBAR";: NORMAL=0:PRINT "> TO START"; : GOSUB 60:IF KEY$ = " " THEN RETURN
591 X = INT ( RND (1) * 9) + 1: GOTO 590



595 REM RECORDS SUBROUTINE (600-721)
599 REM HOME=0:PRINT CLRSCRN$: INVERSE=0:PRINT @(0,0) ABLANK$;:NORMAL=0:PRINT @(0,0) "    RECORDS OF HIGH & LOW PRICES":INVERSE=0:PRINT ABLANK$;:NORMAL=0:PRINT:PRINT "    RECORDS BY P)ORT OR I)TEM? ": PRINT"    __________________________"

600 HOME=0:PRINT CLRSCRN$: INVERSE=0:PRINT @(0,0) ABLANK$;:NORMAL=0:PRINT @(0,1*FONTY) "    RECORDS OF HIGH & LOW PRICES":INVERSE=0:PRINT @(0,2*FONTY) ABLANK$;:NORMAL=0:PRINT  @(0,3*FONTY):PRINT "    RECORDS BY P)ORT OR I)TEM? "

601 PRINT"    __________________________": VTAB=10: PRINT @(0*FONTX,10*FONTY) "    PRESS <SPACEBAR> WHEN FINISHED":PRINT: INVERSE=0: PRINT ABLANK$: NORMAL=0
610 GOSUB 60:IF KEY$ = "P" OR KEY$ = "p" THEN GOSUB 720: GOTO 620
611 IF KEY$ = "I" OR KEY$ = "i" THEN PRINT: VTAB=5:PRINT @(0*FONTX,4*FONTY) ABLANK$: PRINT ABLANK$: GOSUB 660: GOTO 670
612 IF KEY$=" " THEN HOME=0:PRINT CLRSCRN$: RETURN
613 IF KEY$<>"P" AND KEY$<>"I" AND KEY$<>"p" AND KEY$<>"i" AND KEY$ <> " " THEN GOSUB 770: GOTO 610



620 GOSUB 60:IF ASC (KEY$) > 47 AND ASC (KEY$) < 58 THEN X = VAL(KEY$)
621 IF ASC (KEY$) < 48 OR ASC (KEY$) > 57 THEN GOSUB 770:GOTO 620
630 HOME=0:PRINT CLRSCRN$:PRINT @(0,0) " ";LOC$(X);" PRICES (VISITS:";VISITS(X);")": INVERSE=0:PRINT " ITEM              HIGH         LOW     ":NORMAL=0:FOR I = 0 TO 5:PRINT " ";GOODS$(I);:HTAB=21: PRINT TAB(21);
639 REM MOVED NEXT I
640 IF HIGH(X,I) = 0 THEN PRINT " ?";: INVERSE=0: PRINT TAB(29) " ";: NORMAL=0: PRINT "   ?"
641 IF HIGH(X,I) <> 0 THEN PRINT TAB(20) HIGH(X,I);: INVERSE=0: PRINT  TAB(29)" ";: NORMAL=0: PRINT " ";LOW(X,I)
642 NEXT I

650 INVERSE=0: PRINT ABLANK$:NORMAL=0: GOTO 700
660 VTAB=5: PRINT @(0*FONTX,5*FONTY);:FOR I = 0 TO 5:PRINT " ";GOODS$(I),:NEXT I:PRINT:PRINT:PRINT " WHAT ITEM (";TC$; "  ?": PRINT:PRINT ABLANK$: INVERSE=0: PRINT ABLANK$: NORMAL=0: GOSUB 260: RETURN
670 HOME=0:PRINT CLRSCRN$:PRINT @(0,0); " ";GOODS$(X1);"  PRICES": INVERSE=0:PRINT " PORT      VISITS     HIGH    LOW       ":NORMAL=0:PRINT @(0,(3)*FONTY);:FOR I = 0 TO 9:PRINT LOC$(I);

680 VTAB=I+3: HTAB=12:PRINT @(12*FONTX,(I+3)*FONTY) VISITS(I);:IF LOW(I,X1) = 0 THEN HTAB=21: INVERSE=0: PRINT @(21*FONTX,(I+3)*FONTY) " ";: NORMAL=0:PRINT "  ?";: HTAB=32:PRINT @(32*FONTX,(I+3)*FONTY) "?":NEXT I:GOTO 690
681 IF LOW(I,X1)<>0 THEN HTAB=21: INVERSE=0:PRINT @(21*FONTX,(I+3)*FONTY) " ";:NORMAL=0:PRINT "  ";HIGH(I,X1) ;: HTAB=31:PRINT @(31*FONTX,(I+3)*FONTY) " ";LOW(I,X1): NEXT I
690 INVERSE=0: PRINT ABLANK$:NORMAL=0
700 PRINT " PRESS <SPACEBAR> WHEN FINISHED";
710 GOSUB 60:IF KEY$ = " " THEN GOTO 600
711 GOTO 710
715 REM PORT CHOICE SUBROUTINE (720)
720 FOR I=0 TO 9 STEP 2: VTAB=(I / 2) + 4:PRINT @(0*FONTX,((I/2)+4)*FONTY) ABLANK$:VTAB=(I / 2) + 4: PRINT @(0*FONTX,((I/2)+4)*FONTY) I;" ";LOC$(I);: HTAB=20:PRINT TAB(20) I + 1;" ";LOC$(I+1):NEXT I:PRINT

721 PRINT "       WHICH PORT (0-9)?             ": PRINT:INVERSE=0: PRINT ABLANK$: NORMAL=0: RETURN


725 REM EMBARK (730-751)
730 IF SH < 0 THEN VTAB=13:PRINT @(0*FONTX,13*FONTY) "YOUR SHIP IS OVERLOADED, TAIPAN         ";: PRINT ABLANK$:GOSUB 760:GOTO 360
731 IF SH >= 0 THEN HOME=0:PRINT CLRSCRN$: PRINT @(11*FONTX,0);:INVERSE=0:PRINT "EMBARKING":NORMAL=0:PRINT TAB( 9);"FROM " ;LOC$ (L ) : INVERSE=0:PRINT ABLANK$:NORMAL=0:FOR I = 0 TO 9:IF L = I THEN NEXT I:GOTO 740
732 IF L <> I THEN PRINT TAB( 10);I;" ";LOC$(I): NEXT I

740 PRINT: PRINT:INVERSE=0:PRINT ABLANK$:NORMAL=0:PRINT "WHAT PORT O' CALL, TAIPAN (0-9)?"

750 GOSUB 60:IF ASC (KEY$) > 47 AND ASC (KEY$) < 58 AND VAL (KEY$) <> L THEN PO = VAL (KEY$) : GOTO 980
751 GOSUB 770: GOTO 750

755 REM NO CAN DO SUBROUTINE (760)

760 PRINT "\a": GOSUB 780:RETURN

765 REM INPUT ERROR SUBROUTINE (770)
770 PRINT "\a";: RETURN


775 REM DELAY SUBROUTINE (780-783)
780 FOR I=0 TO 400
781 PAUSE 10:IF INKEY$<>"" THEN RETURN
782 REM IF PEEK ( -16364) > 127 THEN POKE -16368,0:RETURN
783 NEXT I: RETURN

785 REM EVENTS SUBROUTINE (790-651)
790 IF K = 1 THEN RETURN
791 K=1:X = 50 + INT ( RND (1) * 100) + 1: GN = INT ( RND (1) * 3) +1:XP = (X + (GN * 50)) * 100:IF C < XP OR RND (1) < .75 THEN GOTO 805
792 GOSUB 1340: VTAB=12:PRINT @(0*FONTX,12*FONTY) " A BROKER OFFERS TO TAKE YOUR": PRINT "VESSEL IN TRADE FOR ONE WITH":PRINT GN + G;" GUNS & ";X + MW;" CAPACITY"




800 PRINT "FOR ";XP;" IN CASH. WILL YOU": PRINT " ACCEPT (<Y> OR <N>)?";:GOSUB 60:VTAB=12:PRINT @(0*FONTX,12*FONTY);:IF KEY$ = "Y" OR KEY$ = "y" THEN C = C - XP:SH = SH + X:MW = MW + X:G = G + GN: GOSUB 130
805 GOSUB 1340
810 IF C > D AND D > 2000 AND RND (1) > .7 THEN GOSUB 1340:VTAB=12:PRINT @(0*FONTX,12*FONTY) "YOU'VE BEEN ATTACKED AND ROBBED BY IRON LOTUS RUFFIANS, TAIPAN!" ; : C = INT (C / 3) :GOSUB 760:GOSUB 130
820 GOSUB 180: GOSUB 860: GOSUB 190:GOSUB 1340:DN = INT ((C / 2) * RND (1))
821 IF RND (1) > .8 AND TR = 0 AND L <> 0 THEN VTAB=12:PRINT @(0*FONTX,12*FONTY) "A MESSENGER FROM ";LY$;" ASKS": PRINT "THAT YOU RETURN TO HONG KONG": PRINT "WITHOUT DELAY, TAIPAN." :GOSUB 780


830 IF C > 100 AND TR = 0 AND L = 0 THEN GOSUB 1340:VTAB=12:PRINT @(0*FONTX,12*FONTY) YS$;", LIEUTENANTS":PRINT "OF THE MARINER, ";LY$;", ASK IF": PRINT "YOU WILL DONATE ";:Q = DN:GOSUB 1330:PRINT "TO THE TEMPLE OF TIN HAU, THE"
831 IF C > 100 AND TR = 0 AND L = 0 THEN PRINT "SEA GODDESS. (Y) OR (N)"
840 IF C > 0 AND TR = 0 AND L = 0 THEN GOSUB 60: IF KEY$ = "Y" OR KEY$ = "y" THEN C = C - DN:TR = 1:GOSUB 130:GOSUB 1340:VTAB=12:PRINT @(0*FONTX,12*FONTY) " ":PRINT " " ;YS$;" THANK": PRINT " YOU, AND DEPART.": PRINT:PRINT ABLANK$;
841 IF C > 0 AND TR = 0 AND L = 0 THEN IF KEY$ <> "Y" AND KEY$ <> "y" THEN GOSUB 1340: VTAB=12:PRINT @(0*FONTX,12*FONTY) " ":PRINT " ";YS$;" DEPART":PRINT "ABRUPTLY IN A CHILLY SILENCE.": PRINT ABLANK$;



850 IF (TR=0 AND L=0) OR (KEY$ = "Y" OR KEY$ = "y") AND L = 0 THEN PRINT: PRINT ABLANK$;:GOSUB 780:VTAB=13:PRINT @(0*FONTX,13*FONTY) ABLANK$:PRINT:PRINT
851 RETURN
855 REM BEARS 'N' BULLS SUBROUTINE (860-910)
860 I = INT ( RND (1) * 6)
870 IF RND (1) < .85 THEN RETURN
880 GP(I) = INT (GP(I) * ( RND (1) * 4) + .5)
890 VTAB=12:PRINT @(0*FONTX,12*FONTY) LOC$(L);" MARKET FORCES HAVE": PRINT "DRIVEN ";GOODS$(I);" PRICES TO ";:PRINT GP(I);: PRINT "!";
900 GOSUB 780
910 RETURN

975 REM VOYAGE (980-1290)
980 HOME=0:PRINT CLRSCRN$: PRINT @(0,0);:INVERSE=0:PRINT ABLANK$:NORMAL=0:PRINT " SEA VOYAGE FROM ";LOC$(L) : PRINT " TO ";LOC$(PO) :INVERSE=0: PRINT ABLANK$: NORMAL=0: GOSUB 780: HOME=0:PRINT CLRSCRN$:ET = ABS (LO(L) - LO(PO))
981 IF (INT ( RND (1) * 50) + 1) > (INT ( RND (1) * ET) + 1) THEN GOTO 1290

985 REM STORM (990-1022)
990 IF RND (1) > .5 THEN GOTO 1030
991 VTAB=13: PRINT @(0*FONTX,13*FONTY) BLANK$;:HTAB=1:PRINT TAB(1) " STORM, TAIPAN!          " : PRINT ABLANK$: GOSUB 760
1000 IF RND (1) > .2 THEN GOTO 1020
1001 VTAB=14:PRINT @(0*FONTX,14*FONTY) " ANY PORT IN A STORM,                 ": GOSUB 760:PO = INT ( RND (1) * 10) :IF PO = L THEN VTAB=14: PRINT @(0*FONTX,14*FONTY) " WE CAN'T MAKE IT,                      ": PRINT "TAIPAN,            ": SR = 0:GOTO 1090
1010 VTAB=14:PRINT @(0*FONTX,14*FONTY) " WE'RE HEADED                           ": PRINT "FOR SHELTER,          ": GOSUB 780
1020 VTAB=14:PRINT @(0*FONTX,14*FONTY) " WE RODE OUT THE                        ": PRINT "STORM, TAIPAN!       ": GOSUB 780
1021 IF RND (1) > .5 THEN GOTO 990
1022 GOTO 1290



1025 REM PIRATES (1030-1281)
1030 HOME=0:PRINT CLRSCRN$:ID = INT ( RND (1) * 2) + 1:GOSUB 5290:P = INT ( RND (1) * (MW / 25) * 2 ^ (ID*2)) * INT ( RND (1) * 3) + 1:HTAB=1:VTAB=17:PRINT @(1*FONTX,17*FONTY) P;" PIRATE CRAFT SIGHTED!": GOSUB 780:GOSUB 1100
1040 IF ID = 1 THEN VTAB=19: PRINT @(0*FONTX,19*FONTY) " LOOKS LIKE INDEPENDENT PIRATES.": GOSUB 780
1041 IF ID <> 1 THEN VTAB=19: PRINT @(0*FONTX,19*FONTY) BLANK$,:HTAB=1: PRINT @(1*FONTX, 19*FONTY)" THERE'S ";LY$;"'S BANNER!": GOSUB 780
1050 GOSUB 1100:IF ID = 1 AND RND (1) > .95 THEN VTAB=19:PRINT @(0*FONTX,19*FONTY) BLANK$;:HTAB=1:PRINT TAB(1) " ";LY$;"'S FLEET DROVE 'EM OFF!": GOSUB 6100:P = P * INT ( RND (1) * 10) + 6:ID = 2:GOSUB 5290:GOSUB 1100:IF TR = 0 THEN GOTO 1070
1060 IF ID = 2 AND TR = 1 THEN VTAB=19:PRINT @(0*FONTX,19*FONTY) "THEY GREET US, AND SAIL OFF.    ": GOSUB 6100:P=0:GOSUB 780:IF RND (1) > .8 THEN GOTO 1030
1061 IF ID = 2 AND TR = 1 THEN GOTO 1290


1070 IF CR = 0 AND TR = 0 AND ID = 2 THEN CR = 1: VTAB=19:PRINT @(0*FONTX,19*FONTY) BLANK$;:HTAB=1:PRINT TAB(1) " ";YS$;" ARE CURSING US!": GOSUB 780
1080 VTAB=19:PRINT @(0*FONTX,19*FONTY) "  THEY'RE FIRING ON US!          ": GOSUB 5380
1081 IF INT ( RND (1) * P) + 1 >INT ( RND (1) * 5) + 1 THEN GOSUB 5540: VTAB=19:PRINT @(0*FONTX,19*FONTY) BLANK$;:HTAB=1:PRINT TAB(1) " THE BUGGERS HIT US!            ":SR = SR - ( RND (1) /(MW / ( INT ( RND (1) * 50) + 1))): GOSUB 780:GOSUB 1100:GOTO 1090
1082 VTAB=19:PRINT @(0*FONTX,19*FONTY) " MISSED US, TAIPAN!        ": GOSUB 780



1090 IF SR < .1 THEN GOSUB 780: VTAB=19:PRINT @(0*FONTX,19*FONTY) " WE'RE GOING UNDER, TAIPAN!      ": GOSUB 6290:KEY$= "":GOTO 1300



1091 GOTO 1120
1095 REM SHIP STATUS SUBROUTINE (1100-1110)
1100 HTAB=1: VTAB=16: INVERSE=0: PRINT @(1*FONTX,16*FONTY) ABLANK$:NORMAL=0:PRINT " GUNS: ";G;"         REPAIR:";:IF SR<0 THEN SR=0
1110 PRINT INT (SR * 100);"%  ":PRINT "SHIPS ENCOUNTERED:";P;"       ": PRINT ABLANK$;:PRINT ABLANK$;:PRINT ABLANK$;:RETURN
1120 VTAB=19:PRINT @(0*FONTX,19*FONTY) " SHALL WE R)UN, F)IGHT, OR P)ARLEY?": GOSUB 60:IF KEY$ = "R" OR KEY$ = "r" THEN X = 1
1121 IF KEY$ = "F" OR KEY$ = "f" THEN X = 2
1122 IF KEY$ = "P" OR KEY$ = "p" THEN X = 3
1123 IF KEY$ <> "R" AND KEY$ <> "F" AND KEY$ <> "P" AND KEY$ <> "r" AND KEY$ <> "f" AND KEY$ <> "p" THEN GOSUB 770:GOTO 1120


1130 ON X GOTO 1140,1160,1200

1140 IF SR <= RND (1) AND INT ( RND (1) * 6) + 1 <= INT ( RND (1) * P) + 1 THEN GOTO 1150
1141 IF SR > RND (1) AND INT ( RND (1) * 6) + 1 > INT ( RND (1) * P) + 1 THEN GOTO 1143
1142 GOTO 1150
1143 GOSUB 6220: VTAB=18:PRINT @(0*FONTX,18*FONTY) "WE ESCAPED FROM 'EM TAIPAN!         ": P=0:GOSUB 780:GOSUB 1100:IF RND (1) > .8 THEN GOTO 1030
1144 GOTO 1290


1150 IF SR > RND (1) AND RND (1) > .6 THEN P = P - INT ( INT (( RND (1) * P) + 1) / 2):GOSUB 6220:GOSUB 5290:VTAB=19:HTAB=1:PRINT @(1*FONTX,19*FONTY) P;" OF 'EM STILL WITH US!                         ":GOSUB 780: GOSUB 1100: GOTO 1050

1151 VTAB=19:PRINT @(0*FONTX,19*FONTY) " CAN'T SHAKE 'EM!                               ":GOSUB 780: GOTO 1050:
1152 REM ***TYPO*** END 1151, NO ':' NEEDED


1160 VTAB=19:PRINT @(0*FONTX,19*FONTY) " WE'RE FIRING ON 'EM!               ":GOSUB 5540:IF INT ( RND (1) * (G + 1)) + 1 > INT (RND (1) * 4)THEN VTAB=19:PRINT @(0*FONTX,19*FONTY) BLANK$;:HTAB=1:PRINT TAB(1) "HIT 'EM!":GOSUB 5680:GOTO 1170
1161 VTAB=19:PRINT @(0*FONTX,19*FONTY) " MISSED 'EM!                        ":GOSUB 780: GOTO 1190
1170 NK = RND (1):IF NK > .4 THEN IF P >= G THEN X = INT ( INT (( RND (1) * (G - 1)) + 1) / 3) + 1
1171 IF NK > .4 AND P < G THEN X = INT ( RND (1) * P)+1


1172 IF NK <= .4 THEN VTAB=19:PRINT @(0*FONTX,19*FONTY) " THEY'RE STILL AFLOAT.": GOSUB 780:GOTO 1190



1180 P=P-X: VTAB=19:PRINT @(0*FONTX,19*FONTY) " SANK ";X;" OF 'EM, TAIPAN!": GOSUB 5830:IF P > 0 THEN GOSUB 5290
1190 GOSUB 1100:IF P = 0 THEN IF RND (1) > .5 THEN GOTO 1270
1191 IF P = 0 THEN VTAB=19:PRINT @(0*FONTX,19*FONTY) " THAT'S ALL FOR THE BUGGERS!": GOSUB 780:IF RND (1) > .8 THEN GOTO 1030
1192 IF P = 0 THEN GOTO 1290
1193 GOSUB 780: GOTO 1050
1200 IF RND (1) < .8 THEN VTAB=19:PRINT @(0*FONTX,19*FONTY) " THEY REFUSE TO PARLEY, TAIPAN.       ": GOSUB 780:GOTO 1050
1201 VTAB=19:PRINT @(0*FONTX,19*FONTY) " THEY AGREE TO DISCUSS TERMS.         ": GOSUB 780



1210 IF P > G / 2 OR RND (1) > .7 THEN VTAB=19:PRINT @(0*FONTX,19*FONTY) " THEY OFFER TO LET US GO IN EXCHANGE FOR";:PRINT "ALL OUR CASH. DO WE ACCEPT, TAIPAN (Y/N)";: GOTO 1220




1211 GOTO 1240
1220 GOSUB 60: VTAB=19:PRINT @(0*FONTX,19*FONTY) ABLANK$;:PRINT ABLANK$;:VTAB=18:PRINT  @(0*FONTX,18*FONTY);:IF KEY$ = "Y" OR KEY$ = "y" THEN IF RND (1) > .2 AND C> 100 + INT ( RND (1) * 2000) THEN P =0:C=0:VTAB=19:PRINT @(0*FONTX,19*FONTY) " THEY TOOK IT & RAN!": GOSUB 780:GOSUB 1100:GOTO 1280
1221 IF KEY$ <> "Y" AND KEY$ <> "y" THEN GOTO 1050
1222 C=0: VTAB=19:PRINT @(0*FONTX,19*FONTY) " THEY STILL INTEND TO FIGHT!": PRINT:PRINT ABLANK$;:GOSUB 780
1230 GOSUB 780: GOTO 1050
1240 VTAB=1: HTAB=13: PRINT @(13*FONTX,1*FONTY) SFL$;:VTAB=19:PRINT @(0*FONTX,19*FONTY) " THEY OFFER TO SURRENDER.     ":GOSUB 780:VTAB=19:PRINT @(0*FONTX,19*FONTY) " DO WE LET THEM GIVE UP (Y/N)?     ":GOSUB 60:IF KEY$ = "Y" OR KEY$ = "y" THEN GOTO 1260
1250 IF RND (1) > .5 AND P > 0THEN E = INT ( RND (1) * P) + 1: P=P-E:GOSUB 6100:VTAB=19:PRINT @(0*FONTX,19*FONTY) BLANK$; :TAB 1:PRINT E;" OF 'EM RAN AWAY!": GOSUB 780:GOSUB 1100:GOSUB 5290:GOTO 1050
1260 IF RND (1) > .7 THEN VTAB=19:PRINT @(0*FONTX,19*FONTY) " THEY'RE PREPARING TO ATTACK!   ":GOSUB 780:GOTO 1050
1265 REM CAPTURE (1270-1281)

1270 VTAB=19:PRINT @(0*FONTX,19*FONTY) " WE'VE CAPTURED A BIGGER SHIP!" : GOSUB 760:VTAB=19:PRINT @(0*FONTX,19*FONTY) " WE'RE TRANSFERRING TO IT NOW.": GOSUB 780
1271 G = G + INT ( RND (1) * (G + 1)) + 1:E = SH + INT ( RND (1) * (SH + 150)) + 1: SH = SH + E:MW = MW + E:P=0
1280 IF SR < .1 THEN GOTO 1300 
1281 IF P > 0 THEN GOTO 1050 
1285 REM ARRIVAL (1290)
1290 CR = 0: SR = 1: L=PO : VISITS(L) = VISITS(L) + 1 : B=0: K=0 : GOSUB 160:HOME=0:PRINT CLRSCRN$:PRINT:INVERSE=0:PRINT @(0,0) ABLANK$: NORMAL=0
1291 PRINT " ARRIVING "; LOC$(L);" AFTER": PRINT " A VOYAGE OF ";ET;" DAYS.": INVERSE=0:PRINT ABLANK$:NORMAL=0: GOSUB 780: HOME=0:PRINT CLRSCRN$:GOTO 120

1295 REM THAT'S ALL FOLKS (1300-1321)
1300 HOME=0:PRINT CLRSCRN$:NW = C + BANK - D:Q = NW / GT: VTAB=4: INVERSE=0: PRINT @(0*FONTX,4*FONTY) ABLANK$;: NORMAL=0: PRINT
1301 PRINT "YOUR SCORE, BASED UPON TIME AND YOUR": PRINT "NET WORTH (EXCLUDING STOCK) IS ";: GOSUB 1330:INVERSE=0: PRINT ABLANK$: NORMAL=0
1305 PRINT " BANK: " BANK: PRINT "CASH: " C: PRINT "Debt: " D: PRINT "GAME TIME: " GT
1310 IF KEY$ = "R" OR KEY$ = "r" THEN PRINT "WOULD YOU LIKE TO PICK UP THIS":PRINT"GAME WHERE YOU LEFT OFF (Y/N)?": GOSUB 60:IF KEY$ = "Y" OR KEY$ = "y" THEN HOME=0:PRINT CLRSCRN$:GOTO 120
1320 GOSUB 1340: VTAB=10:PRINT @(0*FONTX,10*FONTY) ABLANK$: PRINT "DO YOU WISH TO START OVER (Y/N)?": GOSUB 60:IF KEY$ = "Y" OR KEY$ = "y" THEN RUN
1321 END


1325 REM BIG NUMBER SUBROUTINE (1330-1370)
1330 IF ABS (Q) < 1E6 THEN PRINT INT (Q);: NORMAL=0:PRINT "    ": RETURN
1331 IF ABS (Q) < 1E9 THEN Q = Q / 1E6: QUOT$ = " MIL":GOTO 1335
1332 IF ABS (Q) < 1E12 THEN Q = Q / 1E9: QUOT$ = " BIL":GOTO 1335
1333 IF ABS (Q) >= 1E12 THEN Q = Q / 1E12: QUOT$ = " TRL":GOTO 1335
1335 PRINT INT (Q);QUOT$;: NORMAL=0:PRINT "      "
1337 RETURN


1338 REM CLEANUP SUBROUTINE (1340-1370)
1340 FOR I = 12 TO 18
1345 HTAB=1
1350 VTAB=I: PRINT @(1*FONTX,I*FONTY) ABLANK$;
1360 NEXT I
1365 PRINT: VTAB=12:PRINT @(0*FONTX,12*FONTY);
1370 RETURN

2000 REM VISIT BANK ROUTINE (2000-2190)
2005 REM DEPOSIT
2010 REM VTAB 13: HTAB 1
2020 PRINT @(0, 13*FONTY) A$: PRINT @(0, 14*FONTY) A$ : PRINT @(0, 13*FONTY) "HOW MUCH WILL YOU DEPOSIT?"
2020 PRINT @(0, 13*FONTY) A$: PRINT @(0, 14*FONTY) A$ : PRINT @(0, 15*FONTY) A$ :  PRINT @(0, 13*FONTY)"BANK: " BANK:  PRINT @(0, 14*FONTY)"----------------------":  PRINT @(0, 15*FONTY)"HOW MUCH WILL YOU DEPOSIT?"
2030 GOSUB 310
2040 IF KEY$ = "A" THEN PRINT CHR$ (8);: INVERSE:PRINT "ALL"; : NORMAL: NUM = C
2050 IF KEY$ <> "A" THEN NUM = VAL (NUMBER$)
2060 IF NUM <= C THEN BANK = BANK + NUM: C = C - NUM: GOSUB 130: GOTO 2100
2070 GOSUB 760
2100 REM WITHDRAW
2110 REM VTAB 13 : HTAB 1
2120 PRINT @(0, 13*FONTY) A$: PRINT @(0, 14*FONTY) A$ : PRINT @(0, 13*FONTY) "HOW MUCH WILL YOU WITHDRAW?"
2120 PRINT @(0, 13*FONTY) A$: PRINT @(0, 14*FONTY) A$ : PRINT @(0, 15*FONTY) A$ :  PRINT @(0, 13*FONTY)"BANK: " BANK:  PRINT @(0, 14*FONTY)"----------------------":  PRINT @(0, 15*FONTY) "HOW MUCH WILL YOU WITHDRAW?"
2130 GOSUB 310
2140 IF KEY$ = "A" THEN PRINT CHR$ (8);: INVERSE:PRINT "ALL"; : NORMAL: NUM = BANK
2150 IF KEY$ <> "A" THEN NUM = VAL (NUMBER$)
2160 IF NUM <= BANK THEN BANK = BANK - NUM: C = C + NUM: GOSUB 130: GOTO 2190
2170 GOSUB 760
2190 RETURN: REM LEAVE BANK


4995 REM SEA ACTION SUBROUTINES (5000-6360)
4997 REM GRAPHICS INITIALIZATION (5000-5120)
5000 DIM CH$(14),CN$(14 )
5005 FOR V1 = 0 TO 12
5010 READ CH$(V1)
5020 CN$(V1) = CH$(V1)
5030 NEXT V1
5040 DATA    "          :             "
5050 DATA    "       -------          "
5060 DATA    "       -------    \\:    "
5070 DATA    " :/    -------    \\\\    "
5075 DATA    " //    -------    \\\\\\   "
5080 DATA    "///    -------    \\\\\\   "
5085 DATA    "///    -------     \\\\   " 
5090 DATA    "///    -------     :\\   " 
5100 DATA    "//     -------     :    "

5105 DATA    "/:     -------    -:-   "
5110 DATA    " :--      :      :000:  "
5111 DATA    ":   \\------------/   /  "
5112 DATA    " \\                  /   "
5120 RETURN


5290 REM PIRATES ARRIVE (5300-5370)
5300 GOSUB 5950
5305 IF ID <> 1 THEN CH$(0) = "       ===:          "

5310 FOR A = 1 TO 10: VTAB=1:PRINT @(0*FONTX,1*FONTY) ABLANK$;:NEXT A
5320 FOR A = 1 TO 30
5340 FOR B = 0 TO 13
5350 VTAB=B+1: HTAB=40-A:PRINT @((40-A)*FONTX,(B+1)*FONTY) LEFT$ (CH$(B),A),;" ";

5360 NEXT B: NEXT A
5370 RETURN


5380 REM PIRATES FIRE (5360-5440)
5390 FOR A=1 TO INT(RND(1)*6) + 1
5410 A1 = INT ( RND (1) * 10) + 1
5420 IF A1 = 5 THEN GOTO 5410
5430 HTAB=16 + A1: VTAB=11:PRINT @((16+A1)*FONTX,11*FONTY) "*"
5432 PRINT "\a"
5433 HTAB=16 + A1: VTAB=11:PRINT @((16+A1)*FONTX,11*FONTY) " "
5435 NEXT A
5440 RETURN

5540 REM CANNON HIT (5550-5560)
5550 PRINT "\a"
5560 RETURN


5680 REM DAMAGE 'EM (5730-5760)
5730 I = INT ( RND (1) * 17) + 2: I1 = INT ( RND (1) * 9) + 2
5740 CH$(I1) = LEFT$ (CH$(I1),I - 1) + " " + MID$ (CH$(I1) , I + 1, LEN (CH$(I1)))
5750 HTAB=10: VTAB=I1+1: PRINT @(10*FONTX,(I1+1)*FONTY) CH$(I1)
5755 GOSUB 780
5760 RETURN


5830 REM PIRATES SINK (5840-5940)
5840 FOR A = 0 TO 12
5850 VTAB=A+1: PRINT @(0*FONTX,(A+1)*FONTY) ABLANK$;
5870 FOR A1=0 TO 12-A
5880 VTAB=2+A+A1: HTAB=10:PRINT @(10*FONTX,(2+A+A1)*FONTY) CH$(A1)
5890 NEXT A1
5910 NEXT A
5920 VTAB=14: PRINT @(0*FONTX,14*FONTY) ABLANK$;
5930 GOSUB 5950
5940 RETURN


5950 REM REPAIR DAMAGE (5960-5990)
5960 FOR A = 0 TO 13
5970 CH$(A)= CN$(A)

5980 NEXT A
5990 RETURN


6100 REM PIRATES DEPART (6110-6210)
6110 FOR A = 9 TO 0 STEP -1
6130 FOR B = 0 TO 13
6140 HTAB=A+ 1: VTAB=B+1:PRINT @((A+1)*FONTX,(B+1)*FONTY) CH$(B);" "
6150 NEXT B: NEXT A
6160 FOR A = 22 TO 1 STEP -1
6180 FOR B = 0 TO 13
6190 HTAB=1: VTAB=B+1:PRINT @(1*FONTX,(B+1)*FONTY) RIGHT$ (CH$ (B) ,A) ;" "
6200 NEXT B: NEXT A
6210 RETURN


6220 REM WE PULL AWAY (6230-6280)
6230 FOR A = 30 TO 1 STEP -1
6250 FOR B = 0 TO 13
6260 HTAB=40 - A: VTAB=B+1:PRINT @((40-A)*FONTX,(B+1)*FONTY) " "; LEFT$ (CH$(B),A); "\b ";
6270 NEXT B: NEXT A
6260 RETURN



6290 REM WE'RE SUNK! (6300-6360)
6300 FOR A = 1 TO 17
6310 FOR B = 1 TO 100: NEXT B
6320 VTAB=24:HTAB=INT ( RND (1) * 40) + 1: PRINT @((INT ( RND (1) * 40) + 1)*FONTX,24*FONTY) "O"


6350 NEXT A
6360 RETURN
